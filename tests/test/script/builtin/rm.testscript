# file      : tests/test/script/builtin/rm.testscript
# license   : MIT; see accompanying LICENSE file

testscript.syntax = 2

.include ../common.testscript

: dir
:
{{
  : scope
  :
  : Removing scope directory fails.
  :
  $c <<EOI && $b
    testscript.syntax = 2

    rm -r ./ 2>/"rm: 'test/3/' contains test working directory 'test/3/'" == 1
    EOI
}}

: path
:
{{
  : outside-scope
  :
  : Need to use a path that unlikely exists (not to remove something useful).
  :
  {{
    : fail
    :
    : Removing path outside the testscript working directory fails.
    :
    $c <<EOI && $b
      testscript.syntax = 2

      rm ../../a/b/c 2>>/EOE == 1
      rm: 'a/b/c' is out of working directory 'test/'
      EOE
      EOI

    : force
    :
    : Removing path outside the testscript working directory succeeds with -f
    : option.
    :
    $c <<EOI && $b
      testscript.syntax = 2

      rm -f ../../a/b/c
      EOI
  }}
}}

: syntax-1
:
{{
  : dir
  :
  {{
    : scope
    :
    : Removing scope directory fails.
    :
    $c <<EOI && $b
      rm -r ./ 2>/"rm: 'test/1/' contains test working directory 'test/1/'" == 1
      EOI
  }}

  : path
  :
  {{
    : outside-scope
    :
    : Need to use a path that unlikely exists (not to remove something useful).
    :
    {{
      : fail
      :
      : Removing path outside the testscript working directory fails.
      :
      $c <<EOI && $b
        rm ../../a/b/c 2>>/EOE == 1
        rm: 'a/b/c' is out of working directory 'test/'
        EOE
        EOI

      : force
      :
      : Removing path outside the testscript working directory succeeds with -f
      : option.
      :
      $c <'rm -f ../../a/b/c' && $b
    }}
  }}
}}
