# file      : tests/test/script/runner/if.testscript
# license   : MIT; see accompanying LICENSE file

.include ../common.testscript

: if-branch
:
$c <<EOI && $b >'if'
{
  if cat <'foo' >'foo'
    echo 'if' >|
  else
    echo 'else' >|
}
EOI

: else-branch
:
$c <<EOI && $b >'else'
{
  if cat <'foo' >'bar'
    echo 'if' >|
  else
    echo 'else' >|
}
EOI

: syntax-1
:
{{
  b = [cmdline] $regex.apply($b, '(.+testscript.syntax).+', "\1 = 1'")

  : if-branch
  :
  $c <<EOI && $b >'if'
  if cat <'foo' >'foo'
    echo 'if' >|
  else
    echo 'else' >|
  end
  EOI

  : else-branch
  :
  $c <<EOI && $b >'else'
  if cat <'foo' >'bar'
    echo 'if' >|
  else
    echo 'else' >|
  end
  EOI
}}
